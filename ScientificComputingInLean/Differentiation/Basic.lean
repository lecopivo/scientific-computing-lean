import ScientificComputingInLean.Meta
import SciLean

open Verso.Genre Manual

set_option pp.rawOnError true
set_option linter.hashCommand false
set_option linter.haveLet 0

set_option maxHeartbeats 1000000000

open Lean.MessageSeverity
open SciLean


#doc (Manual) "Symbolic Differentiation" =>

Differentiation is at the heart of many problems in scientific computing, from optimizing functions to solving differential equations. Computing derivatives is a mechanical process but very error prone when done manually. In this chapter, we will explore how we can compute derivatives of expressions and programs automatically.

Let's recall what derivative is, for a function `f : â„ â†’ â„` we define it derivative as
```latex
f'(x) = \lim_{h â†’ 0} \frac{f(x+h) - f(x)}{h}
```
The mathematical library *Mathlib* defines derivative {lean}`fderiv` for a general function `f : X â†’ Y` from some vector spaces `X` and `Y`. The derivative of `f` at point `x : X` in direction `dx : X` is
```latex
\texttt{fderiv â„ f x dx} = \lim_{h â†’ 0} \frac{f(x+h\cdot dx) - f(x)}{h}
```

*SciLean* provides tactic `fun_trans` which can compute derivatives. The name `fun_trans` stands for 'function transformation' and differentiations is just one example of function transformation. We will talk about general function transformations later in the book.

One of the simplest examples of a derivative is the derivative of \\(x^n\\) which is equal to \\(n x^\{n-1\}\\). Let's compute it using `fun_trans` tactic
```lean (name:=xnderiv)
variable (n : Nat) (x : â„)
#check (fderiv â„ (fun (x : â„) => x^n) x 1) rewrite_by fun_trans
```
```leanOutput xnderiv
â†‘n * x ^ (n - 1) : â„
```

::: TODO
Add a link to an interactive code editor and encourage reader to differentiate more complicated expressions involving sin, cos, exp, ... but warn about log or division.
:::

# Notation

Writing `fderiv â„ (fun x => f x)` is somewhat tedious so *SciLean* makes our life easier by introducing a nice notation `âˆ‚ x, f x` for differentiating `(f x)` w.r.t. `x`.


Before we explore this notation further we have to mention that  {lean}`fderiv` can also compute complex derivatives with {lean}`@fderiv â„‚` instead of {lean}`@fderiv â„`. However, most of the time we work exclusively with real derivative so we can inform Lean that the default choce of the scalar field are real numbers using the following command
```lean
set_default_scalar â„
```
Now Lean knows that we want real derivative when we write `âˆ‚ x, f x`.


Using this notation we can compute again the above derivative
```lean (name:=nxderiv2)
variable (n : Nat)
#check (âˆ‚ (x : â„), x^n) rewrite_by fun_trans
```
```leanOutput nxderiv2
fun x => â†‘n * x ^ (n - 1) : â„ â†’ â„
```
Because we did not specify the point where we want to compute the derivative we obtained a function in `x`. We can specify the point where we want to compute the derivative with `âˆ‚ (x:=xâ‚€), ...`
```lean (name:=nxderiv3)
variable (n : Nat) (xâ‚€ : â„)
#check (âˆ‚ (x:=xâ‚€), x^n) rewrite_by fun_trans
```
```leanOutput nxderiv3
â†‘n * xâ‚€ ^ (n - 1) : â„
```

Writing `rewrite_by fun_trans` every time we want to diferentiate an expression gets a bit tedious. We can add an exclamation mark after `âˆ‚` to indicate that we want to run `fun_trans` tactic to compute the derivative.
```lean (name:=bangderiv)
variable (n : Nat)
#check (âˆ‚! (x : â„), x^n)
```
```leanOutput bangderiv
fun x => â†‘n * x ^ (n - 1) : â„ â†’ â„
```

We can differentiate w.r.t to a vector valued variable `(x : â„Ã—â„)`
```lean (name:=vecderiv1)
#check âˆ‚! (x : â„Ã—â„), â€–xâ€–â‚‚Â²
```
```leanOutput vecderiv1
fun x => fun dx =>L[â„] 2 * âŸªdx, xâŸ«_â„ : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] â„
```
For derivatives w.r.t. a vector valued variable we have to also specify the direction in which we differentiate. Therefore in the above we obtained derivative as a function of the position `x` and direction `dx`. Furthermore, the notation `fun dx =>L[â„] ...` indicates that the function is linear function in `dx` and similarly `X â†’L[â„] Y` stands for the space of â„-linear functions from `X` to `Y`.

If we want to specify the position and the direction in which we want to compute the derivatives we use the notation `âˆ‚ (x:=xâ‚€;dxâ‚€), f x`
```lean (name:=vecderiv2)
variable (xâ‚€ dxâ‚€ : â„Ã—â„)
#check âˆ‚! (x:=xâ‚€;dxâ‚€), â€–xâ€–â‚‚Â²
```
```leanOutput vecderiv2
2 * âŸªdxâ‚€, xâ‚€âŸ«_â„ : â„
```

To summarize all the different variants. For function of a scalar valued argument
```lean
variable (f : â„ â†’ â„) (xâ‚€ : â„)

#check âˆ‚ f
#check âˆ‚ x, f x
#check âˆ‚ (x:=xâ‚€), f x
```

For function of a vector valued argument
```lean
variable (f : â„Ã—â„ â†’ â„) (xâ‚€ dxâ‚€ : â„Ã—â„)

#check âˆ‚ f
#check âˆ‚ x, f x
#check âˆ‚ (x:=xâ‚€), f x
#check âˆ‚ (x:=xâ‚€;dxâ‚€), f x
```

There is nothing stopping us from applying derivative multiple times to compute higher order derivatives
```lean (name:=sndderiv)
#check (âˆ‚! (âˆ‚! (x:â„), x^2))
```
```leanOutput sndderiv
fun x => 2 : â„ â†’ â„
```


## Exercises

1. Express first derivative of `f : â„ â†’ â„ â†’ â„` in the first and the second argument. Also express derivative in both arguments at the same time.

::: Solution
```lean
variable (f : â„ â†’ â„ â†’ â„) (xâ‚€ yâ‚€ : â„)

-- first argument
#check âˆ‚ (x:=xâ‚€), (f x yâ‚€)
#check âˆ‚ (f Â· yâ‚€) xâ‚€

-- second agument
#check âˆ‚ (y:=yâ‚€), (f xâ‚€ y)
#check âˆ‚ (f xâ‚€ Â·) yâ‚€

-- both arguments
#check âˆ‚ ((x,y):=(xâ‚€,yâ‚€)), f x y
```
:::

2. For `(g : â„Ã—â„ â†’ â„)`, express derivative of `g (x,y)` in `x`.

::: Solution
```lean
variable (g : â„Ã—â„ â†’ â„) (xâ‚€ yâ‚€ : â„)

#check âˆ‚ (xy:=(xâ‚€,yâ‚€);(1,0)), g xy
#check âˆ‚ g (xâ‚€,yâ‚€) (1,0)
#check âˆ‚ (x:=xâ‚€), g (x,yâ‚€)
```
:::

3. Express second derivatives of `f : â„ â†’ â„ â†’ â„` in the first and the second argument.

::: Solution
```lean
variable (f : â„ â†’ â„ â†’ â„) (xâ‚€ yâ‚€ : â„)
#check âˆ‚ (x':= xâ‚€), âˆ‚ (x'':=x'), (f x'' yâ‚€)
#check âˆ‚ (âˆ‚ (f Â· yâ‚€)) xâ‚€

#check âˆ‚ (y':=yâ‚€), âˆ‚ (y'':=y'), (f xâ‚€ y'')
#check âˆ‚ (âˆ‚ (f xâ‚€ Â·)) yâ‚€
```
:::

4. Let \\(L(t,x)\\) be a function of time and space and `y(t)` a function of time. Express \\( \\frac\{d\}\{dt\} L(t, y(t)) \\) and \\( \\frac\{\\partial\}\{\\partial t\} L(t, y(t)) \\) in Lean. What is the difference between these two expressions? 

::: Solution
```lean
variable (L : â„ â†’ â„ â†’ â„) (y : â„ â†’ â„) (t : â„)

-- d/dt L
#check âˆ‚ (t':=t), L t' (y t')

-- âˆ‚/âˆ‚t L
#check âˆ‚ (t':=t), L t' (y t)
```
Because SciLean's notation forces you to be a bit more explicit, there is no need to distinguish between \\( \\frac\{d\}\{dt\} \\) and \\( \\frac\{\\partial\}\{\\partial t\} \\). Lots of pain and suffering has been infliced on generations of physics students because of the ambiguity of partial derivative notation.
:::

4. Express one dimensional Euler-Lagrange equation in Lean

```latex
\frac{d}{dt} \frac{\partial L}{\partial \dot x}(x(t),\dot x(t)) -  \frac{\partial L}{\partial x}(x(t), \dot x(t))
```

::: Solution
```lean
variable (L : â„ â†’ â„ â†’ â„) (x : â„ â†’ â„) (t : â„)

#check 
  let v := âˆ‚ x
  âˆ‚ (t':=t), (âˆ‚ (v':=v t'), L (x t') v') - âˆ‚ (x':=x t), L x' (v t)
```
:::

# Examples

Let's put computing derivatives to some practical use. We will demonstrate how to use *SciLean* symbolic differentiations to solve few common tasks in scientific computing and physics.

## Newton's solver

Solving non-linear equation \\( f(x) = 0 \\) is a very common problem in scientific computing. Often this can be done only approximately and a popular tool to do so is Newton's method. It is an interative process that starts with an initial guess \\(xâ‚€\\) which is incrementally improved by the following rule
```latex
x_{n+1} = x_n - \frac{f(x_n)}{f'(x_n)}
```

We can use Newton's method to compute sqruare root of \\(y\\) by choosing \\( f(x) = x^2 - y\\).
```lean (show := false)
set_default_scalar Float
```
```lean (name := mysqrt)
def mySqrt (steps : Nat) (y : Float) : Float := Id.run do
  let f := fun x => x^2 - y
  let mut x := 1.0
  for _ in [0:steps] do
    x := x - f x / ((deriv f x) rewrite_by fun_trans[deriv])
  return x

#eval mySqrt 10 2.0
```
```leanOutput mysqrt
1.414214
```
::: TODO
In {lean}`mySqrt` we should use `(âˆ‚! f x)` notation but unfortunatelly it is currently broken for some reason.
:::

You might feel a bit unconfortable here are we are differentiating a function defined on floating point numbers. If you think that can't be formally correct then you are right. We will discuss this issue in a later chapter "Working with Real Numbers".


### Exercises

1. try to solve different equations, for example `exp x = y` to obtain `log`, `x*exp x = y` to obtain Lambert W function or some polynomial.

2. measure relative,\\(\\left| \\frac\{f(x\_n)\}\{x\_n\} \\right| \\), and absolute error \\( \\left| f(x\_n) \\right| \\) and use them for stopping criteria.

3. A difficult exercise is to define a general `newtonSolve` function that takes an arbitrary function `f : Float â†’ Float` and uring elaboration synthesizes its derivative. Add multiple hints, 1. use `infer_var` trick, 2. state explicitly how the arguments should look like

::: Solution
```lean
set_default_scalar Float
def newtonSolve (steps : Nat) (xâ‚€ : Float)
    (f : Float â†’ Float) {f' : Float â†’ Float}
    (hf : f' = (âˆ‚ f) := by unfold deriv; fun_trans; infer_var) : Float := Id.run do
  let mut x := xâ‚€
  for _ in [0:steps] do
    x := x - f x / f' x
  return x

#eval newtonSolve 10 1.0 (fun x => x^2 - 2.0)
```
::: 

## Kinematics

```lean (show:=false)
set_default_scalar â„
```

We can also use *SciLean*'s symbolic differentiation to prove some basic theorems from physics. For example we can state the second Newton's law
```lean

def NewtonSecondLaw (m : â„) (x : â„ â†’ â„) (F : â„ â†’ â„) : Prop :=
  âˆ€ t, m * deriv (âˆ‚ x) t = F t
```
saying that for a particle with mass `m` under the influence of force `F` has trajectory `x` if the mass times the acceleration `deriv (âˆ‚ x) t`, i.e. the second derivative of trajectory, is equal to the force `F t`.


We can show that under constant force `f` a particle with mass `m` has trajectory `(fun t => 1/2 * f/m * t^2)`
```lean
example (m f : â„) (hm : m â‰  0) :
    NewtonSecondLaw m (fun t => 1/2 * f/m * t^2) (fun _ => f) := by

  unfold NewtonSecondLaw
  -- compute derivatives
  fun_trans [deriv]
  -- finish algebraic simplifications
  field_simp; ring
```

::: TODO

*Warning*: currently the tactic `fun_trans` uses theorems that are not fully proven therefore the above proof is not completely formal proof. If you do not like this you are welcome to improve SciLean by helping out proving its theorems. Lots of theorems should be just matter of finding the right mathlib theorem.

:::




### Exercises

1. show that trajectory `x := fun t => (cos t, sin t)` satisfies differential equation `âˆ‚ x t = (- (x t).2, (x t).1)`

::: Solution
```lean
open SciLean Scalar
def ode (x : â„ â†’ â„Ã—â„) := âˆ€ t, deriv x t = (- (x t).2, (x t).1)

example : ode (fun t => (cos t, sin t)) := by unfold ode deriv; fun_trans
```
:::

2. Show that trajectory \\(x(t) = \\sin(\\omega t) \\) corresponds to the force \\(f(x) = - k x \\) with \\(\\omega = \\sqrt\{(k/m)\} \\)

::: Hint
After differentiation you will have to show that \\(m \\sqrt\{\\frac\{k\}\{m\}\}^2 = k\\). Unfortunatelly Lean is not yet very powerful computer algebra system. So you can finish the proof with
```
  ring_nf --  m * (sqrt (k / m) * (sqrt (k / m) ==> m * sqrt (k * mâ»Â¹) ^ 2
  have h : m * sqrt (k * mâ»Â¹) ^ 2 = k := sorry_proof
  simp[h]
```
where we call `ring_nf` to clean up the expression, then we just assume that ` m * sqrt (k * mâ»Â¹) ^ 2` is equal to `k` and finally we can finish the proof by running simp
::: 

::: Solution
```lean
open SciLean Scalar

example (m k : â„) :
    let Ï‰ := sqrt (k/m)
    let x := fun t => sin (Ï‰*t)
    NewtonSecondLaw m x (fun t => - k*x t) := by

  unfold NewtonSecondLaw deriv
  fun_trans
  ring_nf
  have h : m * sqrt (k * mâ»Â¹) ^ 2 = k := sorry_proof
  simp[h]
```
:::


3. Show that \\(u(t,x) = sin(x-t)\\) is a solution to the wave equation
```latex
\frac{\partial^2 u}{\partial t^2} = \frac{\partial^2 u}{\partial x^2}
```

::: Solution
```lean
open SciLean Scalar
def WaveEquation (u : â„ â†’ â„ â†’ â„) := 
  âˆ€ x t, (âˆ‚ (âˆ‚ (u Â· x)) t) = (âˆ‚ (âˆ‚ (u t Â·)) x)

example : 
    WaveEquation (fun t x => sin (x - t)) := by
  unfold WaveEquation deriv
  fun_trans

```
:::

4. solution to heat equation


# Gradient

In many practical applications, we need to compute gradient instead of directional derivative. For a function \\(f : \\mathbb\{R\}^n \\rightarrow \\mathbb\{R\} \\) the gradient of \\(f\\) is a vector of all its partial derivatives
```latex
\nabla f = \left(\frac{\partial f}{\partial x_1}, \dots, \frac{\partial f}{\partial x_n} \right)
```

A more general way of defining gradient is through linear map transposition/adjoint. The derivative of a function `(f : X â†’ â„)` at point `x` is a linear map from `X` to `â„`
```lean (name := linmapderiv)
open SciLean
variable {X} [NormedAddCommGroup X] [AdjointSpace â„ X] [CompleteSpace X]
variable (f : X â†’ â„) (x : X)
#check (âˆ‚ f x)
```
```leanOutput linmapderiv
âˆ‚ f x : X â†’L[â„] â„
```

To obtain gradient we take an adjoint and evaluate it at one. This is exactly how gradient is defined.
```lean
variable (f : X â†’ â„) (x : X)
example : (âˆ‡ f x) = adjoint â„ (âˆ‚ f x) 1 := by rfl
```

This coincides with the standard notion of gradient that it is a vector of all its partial derivatives. For example for `n=2` we have
```lean
variable {n : Nat} (f : â„Ã—â„ â†’ â„) (hf : Differentiable â„ f) (x y : â„)
example : (âˆ‡ f (x,y)) = (âˆ‚ (x':=x), f (x',y), âˆ‚ (y':=y), f (x,y')) := sorry_proof
```

::: TODO

*Warning for mathlib users*: SciLean defines its own version of `adjoint` and `gradient`. The reason is that the product type `â„Ã—â„` and function type `Fin n â†’ â„` are not `InnerProductSpace` and therefore it is impossible do use mathlibs `gradient` on functions of type `â„Ã—â„ â†’ â„` or `(Fin n â†’ â„) â†’ â„`. Mathlib's advice is to use `WithLp 2 (â„Ã—â„)` or `EuclidianSpace n` however this is seriously inconvenient for people that just want to write some code.

SciLean solution to this is to introduce new typeclass `AdjointSpace â„ X` that is almost the same as `InnerProductSpace â„ X` but requires that the norm induced by inner product, `â€–xâ€–â‚‚ = âŸªx,xâŸ«`, is topologically equivalent to the norm `â€–xâ€–`. This way we can provide instance of `AdjointSpace â„ (XÃ—Y)` and `AdjointSpace â„ (Î¹ â†’ X)` without causing issues.

:::


Few examples of of computing gradients
```lean (name:=gradfst)
variable (xâ‚€ : â„Ã—â„)
#check âˆ‡! (x:=xâ‚€), x.1
```
```leanOutput gradfst
(1, 0) : â„ Ã— â„
```

```lean (name:=gradnorm2)
variable (xâ‚€ : â„Ã—â„)
#check âˆ‡! (x:=xâ‚€), â€–xâ€–â‚‚Â²
```
```leanOutput gradnorm2
2 â€¢ xâ‚€ : â„ Ã— â„
```

```lean (name:=gradinner)
variable (xâ‚€ y : â„Ã—â„)
#check âˆ‡! (x:=xâ‚€), âŸªx,yâŸ«
```
```leanOutput gradinner
y : â„ Ã— â„
```

## Exercises

1. Compute gradient of `x[0]`, `â€–xâ€–â‚‚Â²`, `âŸªx,yâŸ«` for `x y : Float^[3]` and gradient of `A[0,1]`, `â€–Aâ€–â‚‚Â²`, `âŸªA,BâŸ«` for `A B : Float^[2,2]`. Also evaluate those results for some concrete values.

::: Solution

```lean
set_default_scalar Float

#eval âˆ‡! (x:=âŠ[1.0,2.0,3.0]), x[0]
#eval âˆ‡! (x:=âŠ[1.0,2.0,3.0]), â€–xâ€–â‚‚Â²
#eval âˆ‡! (x:=âŠ[1.0,2.0,3.0]), âŸªx, âŠ[0.0,1.0,0.0]âŸ«

def matrix1 := âŠ[1.0,2.0;3.0,4.0]

#eval âˆ‡! (A:=matrix1), A[0,1]
#eval âˆ‡! (A:=matrix1), â€–Aâ€–â‚‚Â²
#eval âˆ‡! (A:=matrix1), âŸªA, âŠ[0.0,1.0;0.0,0.0]âŸ«
```
:::


2. Previously we computed \\(\\sqrt\{y\}\\) using Newton's method. Similarly we can {lean}`mySqrt` Compute `sqrt y` using gradient descent by minimizing objective function `f := fun x => (x^2 - y)^2`

::: TODO
Add solution to gradient descent
:::

3. Linear regression via gradient descent. Find matrix \\( A \\in \\mathbb\{R\}^\{2\\times 2\} \\) that for given data \\( x\_i, y\_i \\in \\mathbb\{R\}^2 \\) minimizes
```latex
A = \text{argmin}_B \sum_i \| B x_i - y_i \|^2
```

::: Solution

```lean
set_default_scalar Float

def linreg {n : â„•} (x y : Float^[2]^[n]) : Float^[2,2] := 
  let loss := fun (A : Float^[2,2]) =>  
    âˆ‘ i, â€–(âŠ i' => âˆ‘ j, A[i',j] * x[i][j]) - y[i]â€–â‚‚Â²
  sorry
```
:::

4. Write down Euler-Lagrange equation over abstract vector space `X` and show that for lagrangian `L x v := 1/2 * m * â€–vâ€–â‚‚Â² - Ï† x` the Euler-Langran equation is `m * âˆ‚ (âˆ‚ x) t = - âˆ‡ Ï† x`

Either define the Lagrangian over `â„Ã—â„`, `L : â„Ã—â„ â†’ â„Ã—â„ â†’ â„` or you can introduce abstract vector space `X` using this variable command
```
variable {X} [NormedAddCommGroup X] [AdjointSpace â„ X] [CompleteSpace X]
```
The explanation of these typeclasses will be discussed in the last section "Abstract Vector Spaces".

::: Solution
```lean
set_default_scalar â„

noncomputable
def EulerLagrange (L : X â†’ X â†’ â„) (x : â„ â†’ X) (t : â„) :=
  let v := âˆ‚ x
  âˆ‚ (t':=t), (âˆ‡ (v':=v t'), L (x t') v') - âˆ‡ (x':=x t), L x' (v t)

noncomputable
def NewtonsLaw (m : â„) (Ï† : X â†’ â„) (x : â„ â†’ X) (t : â„) :=
  m â€¢ (âˆ‚ (âˆ‚ x) t) + (âˆ‡ Ï† (x t))

-- example 
--     (x : â„ â†’ X) (hx : ContDiff â„ âŠ¤ x)
--     (Ï† : X â†’ â„) (hÏ† : Differentiable â„ Ï†) :
--     EulerLagrange (fun x v => m/2 * â€–vâ€–â‚‚Â² - Ï† x) x t
--     =
--     NewtonsLaw m Ï† x t := by 
--   unfold EulerLagrange NewtonsLaw deriv fgradient; fun_trans [smul_smul]
--   sorry
```
:::


# Derivative Rules

```lean (show:=false)
set_default_scalar â„
```

A commong issue when `fun_trans` is not doing what we expect is that there is a missing differentiation theorem.

For example, if we define a function
```lean
def foo (x : â„) := x^2 + x
```
then nothing happens when we try to differentiate it
```lean (name:=fooderivnothing)
#check âˆ‚! x, foo x
```
```leanOutput fooderivnothing
fun x => (âˆ‚ (x:=x), foo x) 1 : â„ â†’ â„
```
Turning on the `fun_trans` trace reveals useful information
```lean
set_option trace.Meta.Tactic.fun_trans true in
#check âˆ‚! x, foo x
```
```
[Meta.Tactic.fun_trans] [âŒ] âˆ‚ (x:=x), foo x
  [Meta.Tactic.fun_trans] candidate theorems for foo: []
  [Meta.Tactic.fun_trans] candidate local theorems for foo: []
  [Meta.Tactic.fun_trans] candidate fvar theorems: [isContinuousLinearMap_fderiv]
  [Meta.Tactic.fun_trans] [âŒ] applying: isContinuousLinearMap_fderiv
    [Meta.Tactic.fun_trans] isContinuousLinearMap_fderiv, failed to discharge hypotheses
          SciLean.IsContinuousLinearMap â„ fun x => foo x
```

The `âŒ` on the first line signifies that `fun_trans` failed to make prograss on `âˆ‚ (x:=x), foo x`. The next two lines
```
  [Meta.Tactic.fun_trans] candidate theorems for foo: []
  [Meta.Tactic.fun_trans] candidate local theorems for foo: []
```
states that there are no derivative theorems for {lean}`foo`. The next line
```
  [Meta.Tactic.fun_trans] candidate fvar theorems: [isContinuousLinearMap_fderiv]
```
states that there is a potentially applicable theorem {name}`isContinuousLinearMap_fderiv` which can differentiate linear functions. However the next few lines report that applying this theorem failed as `fun_trans` can't prove that `foo` is (continuous) linear map.


To remedy this problem we can define derivative of {lean}`foo`
```lean
def foo_deriv (x : â„) := 2*x + 1
```
and add a theorem that the derivative of {lean}`foo` is equal to {lean}`foo_deriv`
```lean
open SciLean
@[fun_trans]
theorem foo_deriv_rule : 
    fderiv â„ foo 
    = 
    fun x => fun dx =>L[â„] dx â€¢ foo_deriv x := by 
  unfold foo foo_deriv; ext x; fun_trans
```

Because {lean}`foo_deriv_rule` is marked with `fun_trans` attribute it will be used when we try to differentiate `foo` now
```lean (name:=fooderiv)
#check âˆ‚! x, foo x
```
```leanOutput fooderiv
fun x => foo_deriv x : â„ â†’ â„
```


Unfortuantelly there is one more issue, `fun_trans` will do nothing when we try to compose `foo` together
```lean (name:=foofooderiv)
#check âˆ‚! x, foo (foo x)
```
```leanOutput foofooderiv
fun x => (âˆ‚ (x:=x), foo (foo x)) 1 : â„ â†’ â„
```
```lean
set_option trace.Meta.Tactic.fun_trans true in
#check âˆ‚! x, foo (foo x)
```
```
...
  [Meta.Tactic.fun_trans] trying comp theorem SciLean.fderiv.comp_rule
  [Meta.Tactic.fun_trans] [âŒ] applying: SciLean.fderiv.comp_rule
    [Meta.Tactic.fun_trans] SciLean.fderiv.comp_rule, failed to discharge hypotheses
          Differentiable â„ fun x0 => foo x0
...
```
The trace reveals that `fun_trans` tries to apply composition(chain) rule {name}`SciLean.fderiv.comp_rule` but it fails as it can't prove {lean}`Differentiable â„ fun x0 => foo x0`. We need another theorem stating that {lean}`foo` is differentiable function. Mathlib has a tactic `fun_prop` that can prove differentiability and many other function properties like linearity, continuity, measurability etc. and `fun_trans` uses this tactic to ensure it can apply chain rule.

We need to add `fun_prop` theorem for {lean}`foo`
```lean
@[fun_prop]
theorem foo_differentiable : Differentiable â„ foo := by unfold foo; fun_prop
```
Now `fun_trans` knows that {lean}`foo` is differentiable function and can safely apply chain rule
```lean (name:=foofooderivsuccess)
#check (âˆ‚! x, foo (foo x)) rewrite_by simp
```
```leanOutput foofooderivsuccess
fun x => foo_deriv x * foo_deriv (foo x) : â„ â†’ â„
```

Writing these theorems by hand is quite tedious so there is a macro `def_fun_prop` and `def_fun_trans` to help you with writing these theorems
```lean
def_fun_prop : Differentiable â„ foo by unfold foo; fun_prop
def_fun_trans : fderiv â„ foo by unfold foo; fun_trans
```
It generates these theorems and definition
```lean (name:=foodiffrule)
#check foo.arg_x.Differentiable_rule
#print foo.arg_x.fderiv
#check foo.arg_x.fderiv_rule
```
The problem of writing appropriate theorems for `fun_trans` and `fun_prop` is quite involved problem and will be discussed in future chapter.



# Differentiating Division, Log, Sqrt, ...

```lean (show:=false)
set_default_scalar â„
```

So far we have worked with functions that are smooth. However, functions like division, `log`, `sqrt`, `â€–Â·â€–â‚‚` are not differentiable everywhere. We have to be a bit careful with those functions because *SciLean* tries to perform computations that are, at least in principle, fully formalizable. Let's try to differentiate division
```lean (name:=divderiv)
#check âˆ‚! (x:â„), 1/x
```
```leanOutput divderiv
fun x => (âˆ‚ (x:=x), xâ»Â¹) 1 : â„ â†’ â„
```
We did not get expected `-xâ»Â²`. When differentiation, or any tactic, is not doing what we expect we can turn on tracing. Let's try again with tracing on
```lean
set_option trace.Meta.Tactic.fun_trans true in
#check âˆ‚! (x:â„), 1/x
```
and the beggining of the trace is saying that `fun_trans` tried to apply theorem {name}`HDiv.hDiv.arg_a0a1.fderiv_rule_at` however it failed to discharge `x â‰  0`
```
[Meta.Tactic.fun_trans] [âŒ] âˆ‚ (x:=x), 1 / x
  [Meta.Tactic.fun_trans] candidate theorems for HDiv.hDiv: [HDiv.hDiv.arg_a0a1.fderiv_rule_at]
  [Meta.Tactic.fun_trans] [âŒ] applying: HDiv.hDiv.arg_a0a1.fderiv_rule_at
    [Meta.Tactic.fun_trans] [âŒ] discharging: x â‰  0
    [Meta.Tactic.fun_trans] HDiv.hDiv.arg_a0a1.fderiv_rule_at, failed to discharge hypotheses
          x â‰  0
```
This makes sense as division `1/x` is well defined and differentiable only away from zero. Therefore we have to differentiate it at a concrete point that is not equal to zero.
```lean (name:=divderivsucc)
variable (xâ‚€ : â„) (hxâ‚€ : xâ‚€ â‰  0)
#check (âˆ‚ (x:=xâ‚€), 1/x) rewrite_by fun_trans (disch:=assumption)
```
```leanOutput divderivsucc
-(xâ‚€ ^ 2)â»Â¹ : â„
```
We introduced a point `xâ‚€` and assumption `hxâ‚€` that it is not equal to zero. By default `fun_trans` does not see this assumption and we have to provide discharger. A discharger is any tactic that tries to satisfy(discharge) any assumption of the theorems `fun_trans` is using. In this simple case `assumption` tactic is enough as it just looks through the local context and tries to directly apply any existing assumptions.

Using `assumption` is not enough for a more complicated expression
```lean (name:=divsqderiv)
variable (xâ‚€ : â„) (hxâ‚€ : xâ‚€ â‰  0)
#check (âˆ‚ (x:=xâ‚€), 1/x^2) rewrite_by fun_trans (disch:=assumption)
```
```leanOutput divsqderiv
âˆ‚ (x:=xâ‚€), (x ^ 2)â»Â¹ : â„
```
tracing shows
```
[Meta.Tactic.fun_trans] HDiv.hDiv.arg_a0a1.fderiv_rule_at,
  failed to discharge hypotheses xâ‚€ ^ 2 â‰  0
```
We need a tactic that is capable of infering `(xâ‚€^2 â‰  0)` from `(xâ‚€ â‰  0)`. A very general and powerful tactic is `aesop`
```lean (name:=divsqderivsucc)
variable (xâ‚€ : â„) (hxâ‚€ : xâ‚€ â‰  0)
#check (âˆ‚ (x:=xâ‚€), 1/x^2) rewrite_by fun_trans (disch:=aesop)
```
```leanOutput divsqderivsucc
-(2 * xâ‚€) / (xâ‚€ ^ 2) ^ 2 : â„
```


In multiple dimensions we often want to differentiate the norm
```lean
open SciLean Scalar

variable (xâ‚€ : â„Ã—â„) (hxâ‚€ : xâ‚€ â‰  0)

#check (âˆ‡ (x:=xâ‚€), â€–xâ€–â‚‚) rewrite_by unfold fgradient; fun_trans (disch:=assumption)
```
The fact that norm is not differentiable can cause issues. The common practice when writing numerical algorithms is to regularize norm using a small positive `Îµ`.
```lean (name:=smoothnormgrad)
open SciLean Scalar
variable (Îµ : â„) (hÎµ : 0 < Îµ)

#check (âˆ‚ (x:â„Ã—â„), sqrt (Îµ + â€–xâ€–â‚‚Â²)) rewrite_by
  unfold deriv
  fun_trans (disch:=intro x; nlinarith [norm2_nonneg â„ x])
```
```leanOutput smoothnormgrad
fun w => fun x =>L[â„] 2 * âŸªx, wâŸ«_â„ / (2 * sqrt (Îµ + â€–wâ€–â‚‚Â²)) : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] â„
```

::: TODO

Figuring out the right tactic like `intro x; nlinarith [norm2_nonneg â„ x]` can be difficult. Therefore introduce tactic/discharger `unsafe_ad_disch` that assumes commong AD assumptions and reports them back to user.

Create unsafe mode differentiation which assumes that everything works out. Effectivelly this requires discharger that recognize commong goals that should be sorries or postponed.

:::

## Exercises

1. gradient of energy for n-body system, newton's potential, lenard jones potential
  - do it for two particles 
  - do it for n-particles

2. signed distance function 
  - compute gradient of sphere sdf
  - compute mean and gaussian curvature of sphere

  - pick SDF from https://iquilezles.org/articles/distfunctions/
    - most of them involve functions that are not differentiable everywhere
    - compute derivative in unsafe mode
    - figure out the minimal condition under which it is differentiable
      

# Abstract Vector Spaces


In calculus we usually consider only functions \\((f : \\mathbb\{R\}^n \\rightarrow \\mathbb\{R\}^m) \\). The issue is that on computers the type \\( \\mathbb\{R\}^n \\) can have multiple different realizations. For example \\( \\mathbb\{R\}^3 \\) can be modeled by `FloatÃ—FloatÃ—Float`, `Float^[3]` or `FloatÃ—Float^[2]`. They are all equivalent but in code we have to explicitely convert between these types. For this reason it is better to work with abstract vector spaces instead of with \\( \\mathbb\{R\}^n \\).

Fortunately mathlib's derivative {lean}`fderiv` is already defined for a function `(f : X â†’ Y)` between two abstract vector spaces `X` and `Y` over a field `ğ•œ`. Mathlib's way of introducing an abstract vector space is rather involved and we need to spend some time talking about it. This presentation will be rather simplified. For interested reader we provide references at the end of this section that go over mathlib's algebraic stracutes in more detail.

A vector space `X` is a set with operations `+,-,â€¢,0` such that 
```
  âˆ€ (x y z : X), x + (y + z) = (x + y) + z
  âˆ€ (x y : X), x + y = y + x
  âˆ€ (x : X), x + 0 = 0
  âˆ€ (x : X), x + (-x) = 0

  âˆ€ (r s : ğ•œ) (x : X), r â€¢ (s â€¢ x) = (r * s) â€¢ x(
  âˆ€ (x : X), 1 â€¢ x = x
  âˆ€ (r : ğ•œ) (x y : X), r â€¢ (x + y) = r â€¢ x + r â€¢ y
  âˆ€ (r s : ğ•œ) (x : X), (r + s) â€¢ x = r â€¢ x + s â€¢ x
```
in mathlib the axioms talking about addition and negation are captured by the type class {name}`AddCommGroup` and the aximps talking about scalar multiplication are captured by the type class {name}`Module`. Therefore if we want to introduce a new abstract vector space over a field `R` we have to introduce these variables

```lean (show := false)
section AbstractVectroSpacesSec1
```
```lean
variable 
  {ğ•œ : Type} [Field ğ•œ]
  {X : Type} [AddCommGroup X] [Module ğ•œ X]

example (s r : ğ•œ) (x y : X) : 
    (s + r) â€¢ (x + y) = s â€¢ x + r â€¢ x + s â€¢ y + r â€¢ y := by 
  simp only [add_smul,smul_add,add_assoc]
```
```lean (show := false)
end AbstractVectroSpacesSec1
```

When we want to differentiate a function `(f : X â†’ Y)` between two vector spaces we also need that `X` and `Y` are equiped with a norm. For this purpose there is {name}`NormedAddCommGroup` which equips {name}`AddCommGroup` with a norm and guarantees that it compatible with addition and negation, and {name}`NormedSpace` which equips {name}`Module` with a norm and guarentees that it is compatible with scalar multiplication. Furthermore, we have to restric to a filed `ğ•œ` that is either real numbers `â„` or complex numbers `â„‚`. The type class {name}`RCLike` states exactly that. Therefore when we work with derivative in general setting the code usually looks like this
```lean (show := false)
section AbstractVectroSpacesSec2
```
```lean
variable 
  {ğ•œ : Type} [RCLike ğ•œ]
  {X : Type} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
  {Y : Type} [NormedAddCommGroup Y] [NormedSpace ğ•œ Y]

set_default_scalar ğ•œ

example (f g : X â†’ Y) (hf : Differentiable ğ•œ f) (hg : Differentiable ğ•œ g) :
    (âˆ‚ x, (f x + g x)) = (âˆ‚ f) + (âˆ‚ g) := by ext x dx; fun_trans
```
```lean (show := false)
end AbstractVectroSpacesSec2
```


When working with gradients we also need inner product as {name}`adjoint` is defined through inner product. Unfortunately, here we diverge from mathlib a little bit. Mathlib defines {name}`InnerProductSpace` which equips {name}`NormedSpace` with inner product. Understandably {name}`InnerProductSpace` requires that the `âŸªx,xâŸ« = â€–xâ€–Â²` however mathlib made the unfortunate decision by definin norm on produce spaces as `â€–(x,y)â€– = max â€–xâ€– â€–yâ€–` which is incompatible with the inner product structure. Therefore type like `â„Ã—â„` can't be equiped with {name}`InnerProductSpace`. Because of these issues, SciLean introduces {name}`AdjointSpace` which is almost identical to {name}`InnerProductSpace` but it only requires that the norm induced by inner product is equivalend to the existing norm i.e. `âˆƒ (c d : â„âº), âˆ€ x, c * âŸªx,xâŸ« â‰¤ â€–xâ€–^2 â‰¤ d * âŸªx,xâŸ«`. On {name}`AdjointSpace` SciLean  introduces Lâ‚‚-norm `â€–xâ€–â‚‚ := sqrt âŸªx,xâŸ«` which you have seen already and it is the norm you most likely want to use instead of the default norm `â€–xâ€–`. Therfore when we work with gradient in general setting the code usually looks like this
```lean (show := false)
section AbstractVectroSpacesSec3
```
```lean
open SciLean
variable 
  {ğ•œ : Type} [RCLike ğ•œ]
  {X : Type} [NormedAddCommGroup X] [AdjointSpace ğ•œ X] [CompleteSpace X]
  {Y : Type} [NormedAddCommGroup Y] [AdjointSpace ğ•œ Y] [CompleteSpace Y]

set_default_scalar ğ•œ

example (f g : X â†’ Y) (hf : Differentiable ğ•œ f) (hg : Differentiable ğ•œ g) :
    (âˆ‡ x, (f x + g x)) = (âˆ‡ f) + (âˆ‡ g) := by 
  ext x; unfold adjointFDeriv; fun_trans
```
```lean (show := false)
end AbstractVectroSpacesSec3
```



For interested reader we recommend reading the chapter [Hierachies](https://leanprover-community.github.io/mathematics_in_lean/C07_Hierarchies.html) from [Mathematics in Lean](https://leanprover-community.github.io/mathematics_in_lean/index.html) which explains how mathlib approaches algebraic hierachies like monoids, groups or modules. After reading that we recommend reading [Differential Calculus in Normed Spaces](https://leanprover-community.github.io/mathematics_in_lean/C10_Differential_Calculus.html#differential-calculus-in-normed-spaces) which how {name}`NormedSpace` is structured.
